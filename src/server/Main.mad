import type { WebSocket } from "MadServer"

import ByteArray from "ByteArray"
import IO from "IO"
import List from "List"
import { Nothing } from "Maybe"
import Terminal from "Terminal"

import Server from "MadServer"

import { MessageEvent, SignInEvent, parseEvent } from "../common/Event"
import { printRegisteredEvent } from "../common/Registered"



alias User = { pseudo :: String, socket :: WebSocket, socketId :: Integer }

makeUserStore :: a
  -> { addUser :: User -> {}, getUsers :: {} -> List User, removeUser :: Integer -> {} }
makeUserStore = () => {
  users = []

  return {
    addUser: (user) => {
      users = List.append(user, users)
    },
    removeUser: (socketId) => {
      users = List.filter((user) => user.socketId != socketId, users)
    },
    getUsers: () => users,
  }
}

userStore = makeUserStore()


main = () => {
  pipe(
    Server.create,
    Server.ws(
      "/*",
      pipe(
        Server.onDisconnected(
          (ws) => {
            IO.putLine("user disconnected")
            userStore.removeUser(Server.getSocketId(ws))
          },
        ),
        Server.onMessage(
          (ws, data) => pipe(
            ByteArray.toString,
            parseEvent,
            IO.cTrace("event received:"),
            where {
              SignInEvent({ pseudo }) =>
                do {
                  userStore.addUser({ pseudo, socketId: Server.getSocketId(ws), socket: ws })
                  IO.putLine(`${Terminal.text.brightCyan(pseudo)} joined the chatroom`)
                  response = pipe(
                    Server.getSocketId,
                    printRegisteredEvent,
                    ByteArray.fromString,
                  )(ws)
                  Server.send(response, ws)
                }

              MessageEvent(evt) =>
                List.forEach((user) => Server.send(data, user.socket), userStore.getUsers())

              // List.forEach(
              //   (user) => if (Server.getSocketId(ws) != user.socketId) {
              //     Server.send(data, user.socket)
              //   },
              //   userStore.getUsers(),
              // )

              _ =>
                {}
            },
          )(data),
        ),
      ),
    ),
    Server.run(3000),
  )({ ssl: Nothing, verbose: true })
}
